//===== rAthena Script =======================================
//= Guild Prize Giveaway
//===== By: ==================================================
//= AnnieRuru / Mysterious
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Rewards guild masters of castle owners after each
//= War of Emperium session.
//===== Additional Comments: =================================
//= v1.0 - Intitial Re-release [Mysterious]
//= v1.1 - Language structures [Mysterious]
//= v1.2 - Added divisions [Mysterious]
//= v1.3 - Added proper information [Mysterious]
//= v1.4 - Optimized! [Euphy]
//= v1.5 - Little bit of cleaning [Mysterious]
//============================================================

prontera,146,166,4	script	Castle Rewards	871,{

	mes "[Castle Rewards]";
	if (agitcheck() || agitcheck2()) { mes "WoE is currently still in progress."; close; }
	mes "Please select a castle:";
	next;
	for(set .@i,0; .@i<30; set .@i,.@i+1)
		set .@menu$, .@menu$+getcastlename(.Castles$[.@i])+":";
	set .@i, select(.@menu$)-1;
	mes "[Castle Rewards]";
	if (getcastledata(.Castles$[.@i],1) != getcharid(2)) { mes "You aren't the owner of this castle."; close; }
	if (getguildmasterid(getcharid(2)) != getcharid(0)) { mes "Only the guild master can claim the reward."; close; }
	if ($castle_claimed&(1<<.@i)) { mes "You've already claimed this castle's reward!"; close; }
	mes "Here's your reward for conquering "+getcastlename(.Castles$[.@i])+".";
	set $castle_claimed, $castle_claimed | (1<<.@i);
	getitem 7227,500;  //Set your prize here!
	getitem 7711,500;
	getitem 7179,500;
	close;

OnAgitEnd:
	set $castle_claimed, 0;
	end;

OnInit:
	setarray .Castles$[0],
		"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
		"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
		"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
		"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",
		"arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",
		"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05";
	end;
}