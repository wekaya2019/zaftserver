//===== Hercules Script ======================================
//= Euphy's Donation Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic Donation shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop2"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,199,246,6	script	Donation/quest Shop#2	4_M_MOCASS2,{ callfunc "qshop2"; }


// Script Core
//============================================================
-	script	Donation_shop2	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce Donation completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of Donation items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional Donation requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Upper-Headgears","Mid-Headgears","Lower-Headgears","Cards","Accessories";

// -----------------------------------------------------------
//  Donation items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,2469,1,0,0,7445,7,7446,7,7447,7,7448,7,945,300,975,1,733,5,7166,50);
	Add(1,15026,1,0,0,5141,5,7047,100,661,1);
	Add(1,2468,1,0,0,969,50,2286,1,2254,1);
	Add(1,2012,1,0,0,5035,1,5074,1,714,1,999,5);
	Add(1,2861,1,0,0,7198,200,7097,40);
	Add(1,15027,1,0,0,7445,7,7446,7,7447,7,7448,7,945,300,975,1,733,5,7166,50);
	Add(1,15043,1,0,0,5278,1,508,200);
	Add(1,2479,1,0,0,1020,100,983,1,2241,1);
	Add(1,2890,1,0,0,2227,1,978,1,1057,100);
	Add(1,2580,1,0,0,2213,1,983,1,914,200);
	Add(1,15042,1,0,0,2227,1,7038,500);
	Add(1,2886,1,0,0,2227,1,7038,500,978,1);
	Add(1,2477,1,0,0,2227,1,7038,500,982,1,983,1);
	Add(1,2476,1,0,0,2227,1,7038,500,975,1,982,1);
	Add(1,2575,1,0,0,969,8,10016,2,5083,1);
	Add(1,15037,1,0,0,624,1,959,300,544,50,1023,1);

	
	Add(2,2862,1,0,0,2211,1,978,1,7003,300);
	Add(2,15028,1,0,0,2243,1,999,100);
	Add(2,2470,1,0,0,7263,2,2276,1,2239,1,568,150);
	Add(2,2014,1,0,0,10015,1,10007,1,5023,1,975,1);
	Add(2,2471,1,0,0,526,2,7270,1,941,1,10004,1);
	Add(2,1657,1,0,0,706,1,1019,500,7200,10,1049,20,7165,500,1054,2,1024,100);
	Add(2,2891,1,0,0,949,100,706,1,722,1,2213,1);
	Add(2,2480,1,0,0,529,10,530,5,539,20,999,10,538,15);
	Add(2,2581,1,0,0,2279,1,7035,50,526,100);
	Add(2,2578,1,0,0,5016,1,981,1,7317,20);
	Add(2,2887,1,0,0,5120,5,995,50,983,1,720,50);
	Add(2,2478,2284,976,1,1060,300,1022,100,5008,5);
	Add(2,15039,1,0,0,1036,450,949,330,539,120,982,1);
	Add(2,2577,1,0,0,5017,1,2229,1,5014,1,7209,100,7123,100,1036,100,979,1);
	Add(2,2472,1,0,0,949,200,916,100,7063,20,925,1);
	Add(2,16013,1,0,0,4293,1,538,100,530,100,529,50);
	
	Add(3,2859,1,0,0,2248,1,7030,108,7194,108,7120,4);
	Add(3,15025,1,0,0,2295,1,969,1,975,1,999,5);
	Add(3,2467,1,0,0,2608,1,7069,500);
	Add(3,2011,1,0,0,2295,1,969,1,975,1,999,5);
	Add(3,2156,1,0,0,7511,100,7340,200,7023,10);
	Add(3,2864,1,0,0,7301,1887,5114,1,611,10);
	Add(3,16018,1,0,0,5132,1,5068,1,2255,1,7023,5,983,1);
	Add(3,2866,1,0,0,1038,600,7048,40);
	Add(3,15029,1,0,0,1038,600,7048,40,982,1,983,1);
	Add(3,2865,1,0,0,1038,600,7048,40,975,1);
	Add(3,2569,1,0,0,1038,600,7048,40,982,1);
	Add(3,2570,1032,100,7100,100,905,250);
	Add(3,2884,1,0,0,5058,1,2214,1,949,500,914,200,622,50,1974,1);
	Add(3,2475,1,0,0,2233,1,983,1,7206,300,7030,1);
	Add(3,15036,1,0,0,2233,1,983,1,7206,300,7030,1,978,1);
	Add(3,2883,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,983,1);
	Add(3,15030,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,975,1);

	
	Add(4,4399,1,0,0,7179,200);
	Add(4,4372,1,0,0,7179,100);
	Add(4,4365,1,0,0,7179,100);
	Add(4,4359,1,0,0,7179,100);
	Add(4,4357,1,0,0,7179,100);
	Add(4,4367,1,0,0,7179,100);
	Add(4,4363,1,0,0,7179,100);
	Add(4,4361,1,0,0,7179,100);
	
	
	Add(5,8500,1,0,0,7179,500);
	Add(5,8501,1,0,0,7179,500);
	Add(5,2554,1,0,0,975,1,976,1,914,100,7441,150,7065,25,2283,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop2"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop2"+.@i,1;
	npcshopattach "qshop2"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Donation Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Donation Shop]";
				mes "You're missing one or more Donation requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Donation Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has Donated "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Donation reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Donation requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop2"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop2	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "Donation_shop2::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop21	-1,909:-1
-	shop	qshop22	-1,909:-1
-	shop	qshop23	-1,909:-1
-	shop	qshop24	-1,909:-1
-	shop	qshop25	-1,909:-1